//file:noinspection GradlePackageUpdate
import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
}

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives = osArch.startsWith("arm") || osArch.startsWith("aarch64")
                ? "natives-linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}"
                : "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives = osArch.contains("64")
                ? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
                : "natives-windows-x86"
        break
}

group 'nau.mike'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains:annotations:22.0.0'
    annotationProcessor "org.projectlombok:lombok:1.18.22"
    compileOnly "org.projectlombok:lombok:1.18.22"
    testAnnotationProcessor "org.projectlombok:lombok:1.18.22"
    testCompileOnly "org.projectlombok:lombok:1.18.22"

    implementation "org.slf4j:slf4j-simple:1.7.32"

    implementation platform("org.lwjgl:lwjgl-bom:3.2.3")

    implementation "org.lwjgl:lwjgl:3.2.3"
    implementation "org.lwjgl:lwjgl-assimp:3.2.3"
    implementation "org.lwjgl:lwjgl-glfw:3.2.3"
    implementation "org.lwjgl:lwjgl-openal:3.2.3"
    implementation "org.lwjgl:lwjgl-opengl:3.2.3"
    implementation "org.lwjgl:lwjgl-stb:3.2.3"

    runtimeOnly "org.lwjgl:lwjgl:3.2.3:$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-assimp:3.2.3:$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw:3.2.3:$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal:3.2.3:$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl:3.2.3:$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb:3.2.3:$lwjglNatives"

    implementation "org.joml:joml:1.10.2"

    implementation "io.github.spair:imgui-java-app:1.84.1.4"
}
